const std = @import("std");

pub fn build(b: *std.Build) void {
    const Feature = std.Target.riscv.Feature;
    const enabled_features = std.Target.riscv.featureSet(&[_]Feature{
        .m,
    });
    const disabled_features = std.Target.riscv.featureSet(&[_]Feature{
        .a,
        .d,
        .e,
        .f,
        .c,
    });
    // RV64IM
    const target = b.resolveTargetQuery(.{
        .cpu_arch = .riscv64,
        .os_tag = .freestanding,
        .abi = .none,
        .ofmt = .elf,
        .cpu_features_add = enabled_features,
        .cpu_features_sub = disabled_features,
    });
    const optimize = b.standardOptimizeOption(.{
        .preferred_optimize_mode = .ReleaseSmall,
    });

    const kernel = b.addExecutable(.{
        .name = "kernel.elf",
        .root_module = b.addModule("kernel", .{
            .root_source_file = b.path("src/main.zig"),
            .target = target,
            .optimize = optimize,
            .code_model = .medium,
        }),
    });

    const use_qemu = b.option(bool, "qemu", "Use QEMU virt") orelse false;
    const options = b.addOptions();
    options.addOption(bool, "qemu", use_qemu);

    const rootfs_tar = makeRootfsTar(b);
    const rootfs_mod = makeRootfsModule(b, rootfs_tar);
    kernel.root_module.addOptions("build_options", options);
    kernel.root_module.addAnonymousImport("rootfs", .{
        .root_source_file = rootfs_mod,
    });

    kernel.entry = .{ .symbol_name = "_start" };
    kernel.setLinkerScript(b.path("src/kernel.ld"));
    b.installArtifact(kernel);

    const kernel_step = b.step("kernel", "Build the kernel");
    kernel_step.dependOn(&kernel.step);

    // ELF -> BIN
    const elf = kernel.getEmittedBin();
    const bin = elf2bin(b, elf, "kernel.bin");

    // BIN -> HEX
    const hex = bin2hex(b, bin, .{
        .bytes_per_line = 4,
        .basename = "kernel.bin.hex",
    });

    // install
    b.getInstallStep().dependOn(&b.addInstallFile(bin, "kernel.bin").step);
    b.getInstallStep().dependOn(&b.addInstallFile(hex, "kernel.bin.hex").step);
    b.getInstallStep().dependOn(&b.addInstallFile(rootfs_tar, "rootfs.tar").step);

    const qemu_cmd = b.addSystemCommand(&.{
        "qemu-system-riscv64",
        "-machine",
        "virt",
        "-bios",
        "none",
        "-nographic",
        "-serial",
        "mon:stdio",
        "--no-reboot",
        "-kernel",
        b.getInstallPath(.bin, kernel.name),
    });
    qemu_cmd.step.dependOn(b.getInstallStep());

    const run_step = b.step("run", "Run the kernel on QEMU");
    run_step.dependOn(&qemu_cmd.step);
}

fn makeRootfsTar(b: *std.Build) std.Build.LazyPath {
    const tar_cmd = b.addSystemCommand(&.{ "tar", "-cf" });
    tar_cmd.setEnvironmentVariable("COPYFILE_DISABLE", "1"); // macOS
    tar_cmd.setEnvironmentVariable("COPY_EXTENDED_ATTRIBUTES_DISABLE", "1"); // macOS
    const tar_out = tar_cmd.addOutputFileArg("rootfs.tar");
    tar_cmd.addArg("--exclude=.DS_Store"); // macOS
    tar_cmd.addArg("--exclude=._*"); // macOS
    tar_cmd.addArg("-C");
    tar_cmd.addDirectoryArg(b.path("rootfs"));
    tar_cmd.addArg(".");
    return tar_out;
}

fn makeRootfsModule(b: *std.Build, rootfs_tar: std.Build.LazyPath) std.Build.LazyPath {
    const wf = b.addWriteFiles();
    _ = wf.addCopyFile(rootfs_tar, "rootfs.tar");
    return wf.add("rootfs.zig", "pub const data = @embedFile(\"rootfs.tar\");\n");
}

fn elf2bin(b: *std.Build, elf: std.Build.LazyPath, out_basename: []const u8) std.Build.LazyPath {
    const oc = b.addObjCopy(elf, .{
        .basename = out_basename,
        .format = .bin,
    });
    return oc.getOutput();
}

fn bin2hex(
    b: *std.Build,
    bin: std.Build.LazyPath,
    options: struct {
        bytes_per_line: usize = 8,
        basename: []const u8,
    },
) std.Build.LazyPath {
    const step = Bin2HexStep.create(b, bin, options.bytes_per_line, options.basename);
    return step.getOutput();
}

const Bin2HexStep = struct {
    step: std.Build.Step,
    input: std.Build.LazyPath,
    bytes_per_line: usize,
    basename: []const u8,
    output_file: std.Build.GeneratedFile,

    pub fn create(owner: *std.Build, input: std.Build.LazyPath, bytes_per_line: usize, basename: []const u8) *@This() {
        const self = owner.allocator.create(@This()) catch @panic("OOM");
        self.* = .{
            .step = std.Build.Step.init(.{
                .id = .custom,
                .name = owner.fmt("bin2hex {s}", .{input.getDisplayName()}),
                .owner = owner,
                .makeFn = make,
            }),
            .input = input,
            .bytes_per_line = bytes_per_line,
            .basename = basename,
            .output_file = .{ .step = &self.step },
        };
        input.addStepDependencies(&self.step);
        return self;
    }

    pub fn getOutput(self: *const @This()) std.Build.LazyPath {
        return .{
            .generated = .{ .file = &self.output_file },
        };
    }

    fn make(step: *std.Build.Step, _: std.Build.Step.MakeOptions) anyerror!void {
        const self: *@This() = @fieldParentPtr("step", step);
        const b = step.owner;

        var man = step.owner.graph.cache.obtain();
        defer man.deinit();

        man.hash.add(@as(u32, 0x6c3a_19d1));
        man.hash.add(self.bytes_per_line);

        const in_path = self.input.getPath(b);
        _ = try man.addFile(in_path, null);

        if (try step.cacheHitAndWatch(&man)) {
            const digest = man.final();
            self.output_file.path = try b.cache_root.join(b.allocator, &.{ "o", &digest, self.basename });
            return;
        }

        const digest = man.final();
        self.output_file.path = try b.cache_root.join(b.allocator, &.{ "o", &digest, self.basename });

        const cache_dir = b.pathJoin(&.{ "o", &digest });
        b.cache_root.handle.makePath(cache_dir) catch |err| {
            return step.fail("unable to make path '{s}': {s}", .{ cache_dir, @errorName(err) });
        };

        var in_file = if (std.fs.path.isAbsolute(in_path))
            try std.fs.openFileAbsolute(in_path, .{})
        else
            try b.build_root.handle.openFile(in_path, .{});
        defer in_file.close();

        const in_bytes = try in_file.readToEndAlloc(b.allocator, std.math.maxInt(usize));
        defer b.allocator.free(in_bytes);

        const rem = in_bytes.len % self.bytes_per_line;
        const pad = self.bytes_per_line - rem; // rem==0 のとき pad==bytes_per_line
        const padded_len = in_bytes.len + pad;

        const out_rel = b.pathJoin(&.{ "o", &digest, self.basename });
        var out_file = try b.cache_root.handle.createFile(out_rel, .{ .truncate = true });
        defer out_file.close();

        var buffer: [4096]u8 = undefined;
        var writer = out_file.writer(&buffer);

        const w = &writer.interface;

        var i: usize = 0;
        while (i < padded_len) : (i += self.bytes_per_line) {
            var j: usize = 0;
            while (j < self.bytes_per_line) : (j += 1) {
                const idx = i + (self.bytes_per_line - 1 - j);
                const byte: u8 = if (idx < in_bytes.len) in_bytes[idx] else 0;
                try w.print("{x:0>2}", .{byte});
            }
            if (i + self.bytes_per_line < padded_len) try w.writeByte('\n');
        }
        try w.flush();

        try step.writeManifestAndWatch(&man);
    }
};
